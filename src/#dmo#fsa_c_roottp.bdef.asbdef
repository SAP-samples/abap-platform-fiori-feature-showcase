projection implementation in class /dmo/fsa_bp_c_roottp unique;
strict ( 2 );
use draft;
use side effects;

define behavior for /DMO/FSA_C_RootTP alias Root
{
  use create;
  use function GetDefaultsForCreate;

  use update;
  use delete;

  use action copyInstance;
  use action changeProgress;
  use action changeCriticality;
  use action resetTimesChildCreated;
  use action increaseTimesChildCreated;
  use action overwriteTimezone;

  use action selectInstance;
  use function GetDefaultsForSelectInstance;

  use action validateDate;
  use action validateChild;
  use action updateTimes;

  action ( features : instance ) createChildFromRoot result [0..1] entity /DMO/FSA_C_ChildTP;

  use action Prepare;
  use action Edit;
  use action Activate;
  use action Discard;
  use action Resume;

  use association _Chart { create; with draft; }

  use association _Child { create; with draft; }
  use function GetDefaultsForChild;

  side effects {
    action createChildFromRoot affects entity _Child, field TimesChildCreated, permissions ( action resetTimesChildCreated );
  }
}

define behavior for /DMO/FSA_C_ChartTP alias Chart
{
  use update;
  use delete;

  use action updateChart;

  use association _Root { with draft; }
}

define behavior for /DMO/FSA_C_ChildTP alias Child
{
  use update;
  use delete;

  use association _Root { with draft; }
  use association _Grandchild { create; with draft; }
}

define behavior for /DMO/FSA_C_GrandchildTP alias Grandchild
{
  use update;
  use delete;

  use association _Root { with draft; }
  use association _Child { with draft; }
}